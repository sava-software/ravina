ext {
  VERSION = project.findProperty('version')
  VCS_URL = 'https://github.com/sava-software/services'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'maven-publish'

  project.group = 'software.sava'
  project.version = "$VERSION"

  final JLV = JavaLanguageVersion.of(project.findProperty('javaVersion') as Integer ?: 23)

  plugins.withType(JavaPlugin).configureEach {
    java {
      modularity.inferModulePath = true
      toolchain {
        languageVersion = JLV
      }
    }
  }

  repositories {
    maven {
      url = "https://maven.pkg.github.com/comodal/json-iterator"
      credentials {
        username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
        password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
      }
    }
    maven {
      url = "https://maven.pkg.github.com/sava-software/sava"
      credentials {
        username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
        password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
      }
    }
    maven {
      url = "https://maven.pkg.github.com/sava-software/solana-programs"
      credentials {
        username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
        password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
      }
    }
    maven {
      url = "https://maven.pkg.github.com/sava-software/anchor-src-gen"
      credentials {
        username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
        password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
      }
    }
    maven {
      url = "https://maven.pkg.github.com/sava-software/anchor-programs"
      credentials {
        username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
        password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
      }
    }
    mavenCentral()
  }

  dependencies {
    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  }

  test {
    useJUnitPlatform()
    testLogging {
      events "passed", "skipped", "failed", "standardOut", "standardError"
      exceptionFormat = "full"
      showStandardStreams = true
    }
  }

  tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
  }

  java {
    withJavadocJar()
    withSourcesJar()
  }

  tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java

        groupId = project.group
        artifactId = project.name + '-services'
        version = project.version

        pom {
          name = project.name + '-services'
          description = "Components for robust Solana Services"
          url = "$VCS_URL"
          licenses {
            license {
              name = 'GNU General Public License v3.0'
              url = 'https://github.com/sava-software/services/blob/main/LICENSE'
            }
          }
          developers {
            developer {
              name = 'Jim'
              id = 'jpe7s'
              email = 'jpe7s.salt188@passfwd.com'
              organization = 'Sava Software'
              organizationUrl = 'https://github.com/sava-software'
            }
          }
          scm {
            connection = 'scm:git:git@github.com:sava-software/services.git'
            developerConnection = 'scm:git:ssh@github.com:sava-software/services.git'
            url = "$VCS_URL"
          }
        }
      }
    }

    repositories {
      maven {
        name = "GitHubPackages"
        url = "https://maven.pkg.github.com/sava-software/services"
        credentials {
          username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user.write")
          password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.token.write")
        }
      }
    }
  }
}
