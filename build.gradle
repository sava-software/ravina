ext {
  VERSION = project.findProperty('version')
  VCS_URL = 'https://github.com/sava-software/ravina'
}

final JLV = JavaLanguageVersion.of(project.findProperty('javaVersion') as Integer ?: 23)
final GPR_USER = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
final GPR_TOKEN = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")

subprojects {
  apply plugin: 'java'
  apply plugin: 'maven-publish'

  project.group = 'software.sava'
  project.version = "$VERSION"

  plugins.withType(JavaPlugin).configureEach {
    java {
      modularity.inferModulePath = true
      toolchain {
        languageVersion = JLV
      }
    }
  }

  repositories {
    maven {
      url = "https://maven.pkg.github.com/comodal/json-iterator"
      credentials {
        username = GPR_USER
        password = GPR_TOKEN
      }
    }
    maven {
      url = "https://maven.pkg.github.com/sava-software/sava"
      credentials {
        username = GPR_USER
        password = GPR_TOKEN
      }
    }
    maven {
      url = "https://maven.pkg.github.com/sava-software/solana-programs"
      credentials {
        username = GPR_USER
        password = GPR_TOKEN
      }
    }
    maven {
      url = "https://maven.pkg.github.com/sava-software/anchor-src-gen"
      credentials {
        username = GPR_USER
        password = GPR_TOKEN
      }
    }
    maven {
      url = "https://maven.pkg.github.com/sava-software/anchor-programs"
      credentials {
        username = GPR_USER
        password = GPR_TOKEN
      }
    }
    mavenCentral()
  }

  dependencies {
    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  }

  test {
    useJUnitPlatform()
    testLogging {
      events "passed", "skipped", "failed", "standardOut", "standardError"
      exceptionFormat = "full"
      showStandardStreams = true
    }
  }

  tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
  }

  java {
    withJavadocJar()
    withSourcesJar()
  }

  tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java

        groupId = project.group
        artifactId = 'ravina-' + project.name
        version = project.version

        pom {
          name = 'ravina-' + project.name
          description = "Components for robust Solana Services"
          url = "$VCS_URL"
          licenses {
            license {
              name = 'GNU General Public License v3.0'
              url = 'https://github.com/sava-software/ravina/blob/main/LICENSE'
            }
          }
          developers {
            developer {
              name = 'Jim'
              id = 'jpe7s'
              email = 'jpe7s.salt188@passfwd.com'
              organization = 'Sava Software'
              organizationUrl = 'https://github.com/sava-software'
            }
          }
          scm {
            connection = 'scm:git:git@github.com:sava-software/ravina.git'
            developerConnection = 'scm:git:ssh@github.com:sava-software/ravina.git'
            url = "$VCS_URL"
          }
        }
      }
    }

    repositories {
      maven {
        name = "GitHubPackages"
        url = "https://maven.pkg.github.com/sava-software/ravina"
        credentials {
          username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user.write")
          password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.token.write")
        }
      }
    }
  }
}
