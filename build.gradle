buildscript {
  repositories {
    maven { url = 'https://plugins.gradle.org/m2/' }
    mavenCentral()
  }
  dependencies {
    // https://plugins.gradle.org/plugin/org.beryx.jlink
    classpath "org.beryx:badass-jlink-plugin:3.0.1"
  }
}

def getAppVersionUnchecked = { ->
  if (project.hasProperty("noVersionTag")) {
    return ''
  }
  try (final var gitTagOut = new ByteArrayOutputStream()) {
    exec {
      commandLine 'git', 'tag', '--points-at', 'HEAD'
      standardOutput = gitTagOut
    }
    final var tagName = gitTagOut.toString().strip()
    if (tagName.isBlank()) {
      try (final var gitHashOut = new ByteArrayOutputStream()) {
        exec {
          commandLine 'git', 'rev-parse', '--short', 'HEAD'
          standardOutput = gitHashOut
        }
        return 'git-' + gitHashOut.toString().strip()
      }
    } else {
      return tagName
    }
  }
} as Object

def getAppVersion = { ->
  if (project.hasProperty("noVersionTag")) {
    return ''
  }
  return getAppVersionUnchecked();
} as Object


ext {
  VERSION = getAppVersion()
  VCS_URL = 'https://github.com/sava-software/services'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'maven-publish'

  project.group = 'software.sava'
  project.version = "$VERSION"

  final JLV = JavaLanguageVersion.of(project.findProperty('targetJava') as Integer ?: 23)

  plugins.withType(JavaPlugin).configureEach {
    java {
      modularity.inferModulePath = true
      toolchain {
        languageVersion = JLV
      }
    }
  }

  repositories {
    maven {
      url = "https://maven.pkg.github.com/comodal/json-iterator"
      credentials {
        username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
        password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
      }
    }
    maven {
      url = "https://maven.pkg.github.com/sava-software/sava"
      credentials {
        username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
        password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
      }
    }
    maven {
      url = "https://maven.pkg.github.com/sava-software/solana-programs"
      credentials {
        username = project.findProperty("gpr.sava.user") ?: System.getenv("GITHUB_SAVA_USER")
        password = project.findProperty("gpr.sava.token") ?: System.getenv("GITHUB_SAVA_TOKEN")
      }
    }
    maven {
      url = "https://maven.pkg.github.com/sava-software/anchor-src-gen"
      credentials {
        username = project.findProperty("gpr.sava.user") ?: System.getenv("GITHUB_SAVA_USER")
        password = project.findProperty("gpr.sava.token") ?: System.getenv("GITHUB_SAVA_TOKEN")
      }
    }
    maven {
      url = "https://maven.pkg.github.com/sava-software/anchor-programs"
      credentials {
        username = project.findProperty("gpr.sava.user") ?: System.getenv("GITHUB_SAVA_USER")
        password = project.findProperty("gpr.sava.token") ?: System.getenv("GITHUB_SAVA_TOKEN")
      }
    }
    mavenCentral()
  }

  dependencies {
    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  }

  test {
    useJUnitPlatform()
    testLogging {
      events "passed", "skipped", "failed", "standardOut", "standardError"
      exceptionFormat "full"
      showStandardStreams true
    }
  }

  tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
        artifact sourcesJar
        groupId project.group
        artifactId project.name + '-services'
        version = project.version
        pom {
          name = project.name + '-services'
          url = "$VCS_URL"
          scm {
            connection = 'scm:git:git@github.com:sava-software/services.git'
            url = "$VCS_URL"
          }
        }
      }
    }
    repositories {
      maven {
        name = "GitHubPackages"
        url = "https://maven.pkg.github.com/sava-software/services"
        credentials {
          username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user.write")
          password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.token.write")
        }
      }
    }
  }
}