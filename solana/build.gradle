apply plugin: 'org.beryx.jlink'

dependencies {
  implementation project(":core")
  implementation libs.bouncycastle
  implementation libs.json.iterator
  implementation libs.sava.core
  implementation libs.sava.rpc
  implementation libs.sava.solana.web2
  implementation libs.sava.solana.programs
  implementation libs.sava.anchor.src.gen
  implementation libs.sava.anchor.programs
}

tasks.register('runSolanaService', JavaExec) {
  classpath = sourceSets.main.runtimeClasspath
  mainClass = project.findProperty('serviceMainClass') as String
  var args = project.findProperty('jvmArgs');
  if (args != null) {
    jvmArgs = List.of((args as String).split('\\s+'))
  }
}

afterEvaluate {
  jlink {
    imageName.set(project.name)
    // MODULES="java.base java.logging java.security.sasl java.naming java.net.http java.transaction.xa java.xml java.sql jdk.httpserver org.bouncycastle.provider software.sava.core systems.comodal.json_iterator software.sava.rpc software.sava.anchor_src_gen software.sava.solana_programs software.sava.anchor_programs software.sava.core_services software.sava.merged.module software.sava.solana_services"
    options.addAll(List.of(
        '--limit-modules=java.base,java.net.http,jdk.httpserver,org.bouncycastle.provider,software.sava.core,systems.comodal.json_iterator,software.sava.rpc,software.sava.anchor_src_gen,software.sava.solana_programs,software.sava.anchor_programs,software.sava.core_services,software.sava.merged.module,software.sava.solana_services',
        '--bind-services',
        '--no-man-pages',
        '--no-header-files',
        '--strip-debug',
        '--compress=1',
        '--ignore-signing-information'
    ))
    enableCds()
  }
}